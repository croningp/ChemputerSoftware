# General constants


# increased the scale to three times
# 3*2.5 G of bromide (36.0 mmol) to the flask
DEF volume_sodium_azide = 75;  #mL 2.535G (39.0 mmol) in 75 mL of water
DEF volume_methyl_propiolate = 3.21; #mL (36.0 mmol)
DEF volume_ammonia = 60; # mL 25% aqueous solution of ammonia  

DEF rufiamide_stirring_rate = 400; # RPM
DEF rufinamide_stirring_rate_2 = 700; # RPM

DEF wait_time_rufinamide_stage_1 = 43200; # 12 h
DEF wait_time_rufinamide_stage_2 = 14400; # 4 h
DEF wait_time_rufinamide_stage_3 = 12; # 12 h

DEF stage_1_temperature = 75; # oC
DEF stage_2_temperature = 65; # oC
DEF stage_3_temperature = 75; # oC
DEF room_temperature = 25; # oC

DEF default_speed_slow = 20;  # mL/min
DEF default_speed_fast = 50;  # mL/min


DEF clean_reactor(){
    S MOVE(flask_acetone, reactor_reactor, 200);
    S SET_STIR_RPM(reactor_reactor, 400);
    S START_STIR(reactor_reactor);
    S WAIT(60);
    S STOP_STIR(reactor_reactor);
    S MOVE(reactor_reactor, waste_reactor, all);
    S MOVE(flask_ether, reactor_reactor, 200);
    S SET_STIR_RPM(reactor_reactor, 400);
    S START_STIR(reactor_reactor);
    S WAIT(60);
    S STOP_STIR(reactor_reactor);
    S MOVE(reactor_reactor, waste_reactor, all);
}



MAIN{
	# 
	#####################################
	# First step of rufinamide synthesis#
	#####################################
	# ADD BROMIDE TO THE FLASK BEFORE RUNNING THE SEQUENCE!
	# Prime the pumps
	S MOVE(flask_water, waste_aqueous, 5, default_speed_slow);
	S MOVE(flask_sodium_azide, waste_aqueous, 5, default_speed_slow);
	
	# Add sodium azide and wash with water
	S MOVE (flask_sodium_azide, reactor_reactor, volume_sodium_azide, default_speed_slow);
	S MOVE(flask_water, reactor_reactor, 2.5); #clean tubing with water
	S MOVE(flask_water, reactor_reactor, 2.5); #clean tubing with water
	
	S SET_STIR_RPM(reactor_reactor, rufiamide_stirring_rate); # set stirring rate
	S START_STIR(reactor_reactor); # start stirrer
	
	S SET_TEMP(reactor_reactor, stage_1_temperature); # set temperature
	S START_HEAT(reactor_reactor); # start heating 
	S WAIT(wait_time_rufinamide_stage_1); # wait for synthesis 
	S STOP_HEAT(reactor_reactor); # stop heater
	S WAIT(1500); # Change to cooling routine to room temp in the future
	S STOP_STIR(reactor_reactor);
	S BREAKPOINT();
	
	
	######################################
	# Second step of rufinamide synthesis#
	######################################
	S MOVE(flask_water, flask_filter_bottom, 3, default_speed_slow); # fill the bottom of the sinter with solvent
	S MOVE(flask_methyl_propiolate, waste_solvents, 3, default_speed_slow); # prime the line with methyl propiolate

	S SET_STIR_RPM(reactor_reactor, rufinamide_stirring_rate_2); # set stirring rate
	S START_STIR(reactor_reactor); # start stirrer
	S SET_STIR_RPM(filter_filter_top, rufiamide_stirring_rate); # Set the stirring rate
	S START_STIR(filter_filter_top); # Start stirrer

	S WAIT(120); # Mix the reaction mixture
	S MOVE(reactor_reactor, filter_filter_top, 200); # Move reaction mixture from reactor to filtration nutche
	S STOP_STIR(reactor_reactor);

	S MOVE (flask_methyl_propiolate, filter_filter_top, volume_methyl_propiolate, default_speed_slow); # Add methyl propiolate
	S MOVE(flask_water, filter_filter_top, 2.5); #clean tubing with water
	S MOVE(flask_water, filter_filter_top, 2.5); #clean tubing with water
	
	S SET_CHILLER(filter_filter_top, stage_2_temperature); # Set the temperature of the chiller
	S START_CHILLER(filter_filter_top); # Starts the chiller
	
	
	S WAIT(wait_time_rufinamide_stage_2); # Wait for reaction to complete
		
	S SET_CHILLER(filter_filter_top, room_temperature);
	S MOVE(flask_water, filter_filter_bottom, 3, default_speed_slow); # fill the bottom of the sinter with solvent
	S WAIT(1800); # Change to cooling routine to room temp in the future
	S STOP_CHILLER(filter_filter_top); # 
	S STOP_STIR(filter_filter_top);
	S BREAKPOINT();
	
	######################################
	# Third step of rufinamide synthesis #
	######################################
	S MOVE(flask_water, flask_filter_bottom, 3, default_speed_slow); # fill the bottom of the sinter with solvent
	# prime the pump with ammonia
	S MOVE(flask_ammonia, waste_aqueous, 5, default_speed_slow);
	
	S SET_STIR_RPM(filter_filter_top, rufinamide_stirring_rate_2); # Set the stirring rate
	S START_STIR(filter_filter_top); # Start stirrer
	
	S MOVE (flask_ammonia, filter_filter_top, volume_ammonia, default_speed_slow);
	S MOVE(flask_water, filter_filter_top, 2.5, default_speed_slow); #clean tubing with water
	S MOVE(flask_water, filter_filter_top, 2.5, default_speed_slow); #clean tubing with water
	
	
	
	S SET_CHILLER(filter_filter_top, stage_3_temperature); # Set the temperature of chiller
	S START_CHILLER(filter_filter_top);
	# wait for 12 hours after each hour add 2 mL of the solvent
	# from below to prevent cloging
	FOR(12)
	{
	S WAIT(3600);
	S MOVE(flask_water, flask_filter_bottom, 2, default_speed_slow); # fill the bottom of the sinter with solvent
	}

	S SET_CHILLER(filter_filter_top, room_temperature); # Cool to room temperature
	# Wait for 1800 s
	FOR(10)
	{
	S WAIT(180); # Change to cooling routine to room temp in the future
	S MOVE(flask_water, flask_filter_bottom, 1, default_speed_slow); # fill the bottom of the sinter with solvent
	}
	
	S STOP_CHILLER(filter_filter_top); #Stop chiller
	S STOP_STIR(filter_filter_top); # Stop stirrer
	S BREAKPOINT();


	S MOVE(flask_filter_bottom, waste_solvents, 120, default_speed_slow); # Remove the solvent
	# Wash with water
	FOR(3) {
			S MOVE(flask_water, filter_filter_top, 20); # wash with water three times
			S MOVE(flask_filter_bottom, waste_solvents, 40);
			}
	# Add Reactor cleaning routine
	S BREAKPOINT();
	
}